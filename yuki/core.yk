(do
  (comptime
   (set-macro! (quote define-macro)
			   (named-lambda define-macro (name args body)
							 (list (quote comptime)
								   (list (quote set-macro!)
										 (list (quote quote) name)
										 (list (quote named-lambda)
											   name
											   args
											   body))))))

  (define-macro defun (name args . body)
	(list (quote do)
		  (list (quote comptime)
				(list (quote register-function!)
					  (list (quote quote) name)
					  (lambda-list-length args)))
		  (list (quote set-global!)
				(list (quote quote) name)
				(list (quote named-lambda)
					  name
					  args
					  (: (quote do)
						 body)))
		  (list (quote quote) name)))

  (comptime
   (defun cond-helper (clauses)
	 (if (null? clauses)
		 nil
		 (let ((c (head clauses)))
		   (list (quote if) (head c)
				 (second c)
				 (cond-helper (tail clauses))))))

   (defun map-helper (fn lst acc)
	 (if (null? lst)
		 (reverse! acc)
		 (map-helper fn (tail lst)
					 (: (fn (head lst)) acc))))

   (defun map (fn lst)
	 (map-helper fn lst nil))

   (defun quasiquote-helper (expression)
	 (if (list? expression)
		 (if (eq? (head expression) (quote unquote))
			 (second expression)
			 (if (null? expression)
				 nil
				 (: (quote list)
					(map quasiquote-helper expression))))
		 (list (quote quote) expression))))

  (define-macro quasiquote (expression)
	(quasiquote-helper expression))

  (define-macro cond clauses
	(cond-helper clauses))

  (define-macro lambda (args . body)
	(append (list (quote named-lambda)
				  (quote anon)
				  args)
			body))

  (define-macro and (a b)
	(quasiquote
	 (if (unquote a) (unquote b) nil)))

  (define-macro define (symbol value)
	(quasiquote
	 (do (comptime (register-global! (quote (unquote symbol))))
		 (set-global! (quote (unquote symbol)) (unquote value)))))

  (defun range-helper (max acc)
	(if (= max 0)
		(: max acc)
		(range-helper (- max 1)
					  (: max acc))))

  (defun range (x) (range-helper x nil))

  (defun assert-fn (value error)
	(if value value
		(invoke-debugger (quote assertion-error))))

  (define-macro assert (value)
	(quasiquote
	 (assert-fn (unquote value)
				(quote (unquote value)))))

  (assert (= 5 5))
  (assert (= 5.0 5))
  (assert (= 5 5.0))
  (assert (= 5.0 5.0))

  (assert (> 4.5 2))
  (assert (> 6 3.2))
  (assert (> 6 5))
  (assert (> 6.5 5.5))

  (assert (< 2 4.5))
  (assert (< 3 6.2))
  (assert (< 5 6))
  (assert (< 5.5 6.5)))
